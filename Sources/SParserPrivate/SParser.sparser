imports
  Foundation
  SParserLibs
syntax
  type
    public Syntax
  ::= importRule rules
    return Syntax(imports: importRule, rules: rules)
  ::= rules
    return Syntax(imports: [], rules: rules)
importRule
  type
    private [String]
  ::= "imports\n" indent imports dedent
    return imports
imports
  type
    private [String]
  ::= importFramework imports
    return [importFramework] + imports
  ::=
    return []
importFramework
  type
    private String
  ::= line
    return line
rules
  type
    private [Rule]
  ::= rules rule
    return rules + [rule]
  ::=
    return []
rule
  type
    private Rule
  ::= name "\n" indent "type\n" indent accessLevel type:line dedent patterns dedent
    return Rule(name: name, accessLevel: accessLevel, type: type, patterns: patterns)
accessLevel
  type
    private AccessLevel
  ::= "public "
    return .public
  ::= "internal "
    return .internal
  ::= "private "
    return .private
  ::=
    return .internal
patterns
  type
    private [Pattern]
  ::= patterns pattern
    return patterns + [Pattern(terms: pattern.terms, evaluator: pattern.evaluator, id: patterns.count)]
  ::=
    return []
pattern
  type
    private Pattern
  ::= "::=" cws terms "\n" multiLineString
    return Pattern(terms: terms, evaluator: multiLineString, id: 0)
terms
  type
    private [Term]
  ::= term cws terms
    return [term] + terms
  ::=
    return []
term
  type
    private Term
  ::= "indent"
    return .indent
  ::= "dedent"
    return .dedent
  ::= name 
    return .type(name: nil, type: name)
  ::= name ":" name
    return .type(name: name, type: name1)
  ::= quotedString
    return .quoted(quotedString)
name
  type
    private String
  ::= letter letterDigits
    return String(letter) + letterDigits

